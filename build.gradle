// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext.videoKitVersion = "1.2.0"

    repositories {
        google()
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:4.1.3"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.4.31"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        mavenLocal()
        maven { url("https://cdn.video.io/android/maven") }
        mavenCentral()
        // maven { url("https://s01.oss.sonatype.org/content/repositories/snapshots/") }
        google()
        jcenter()
        maven { url("https://jitpack.io") }
    }
}

subprojects {
    configurations.configureEach {
        resolutionStrategy { cacheChangingModulesFor 0, 'seconds' }
    }

    plugins.withId("com.android.application") {
        def keystorePropertiesFile = rootProject.file("keystore.properties")
        if (keystorePropertiesFile.exists()) {
            def keystoreProperties = new Properties()
            keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
            android {
                signingConfigs {
                    config {
                        keyAlias keystoreProperties['signing.key']
                        keyPassword keystoreProperties['signing.password']
                        storeFile rootProject.file('keystore')
                        storePassword keystoreProperties['signing.store.password']
                    }
                }
                buildTypes {
                    release {
                        signingConfig signingConfigs.config
                    }
                }
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}